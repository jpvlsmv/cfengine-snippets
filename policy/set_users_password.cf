#!/usr/local/sbin/cf-agent -bset_users_password
# A snippet bundle that sets up a (JSON) collection of users

bundle agent set_users_password
{
	meta:
		# With this tag, placing this file in 
		# $masterfiles/services/autorun/ will enforce this policy 
		# automatically
		"tags" slist => { "autorun" } ;
	
	vars:

	  # the ${passwords_*} variable holds an array of hashes in cfengine
	  # data format.

	  # Example of an inline data definition
	  "passwords_sysadmins" data => parsejson('
	  	[
			{ "user": "joe",
			  "hash": "*KRB*", 
			  "uid" : "1357",
			  "promisee": "sysadmin team"
			  "note": "This sets the hash for joe to force 
			  	   kerberos (Windows AD in our case) 
				   as the only way to log in"
			}, 
			{ "user": "bob",
			  "hash": "*LCK*",
			  "uid" : "1354",
			  "promisee": "sysadmin team"
			  "note": "Bob is no longer with the company,
			  	   so lock his account by setting an invalid
				   hash.
				   Note: we could also enforce this via another
				   user promise with policy => locked"
			} 
		]
	  ');


	  # Example of a JSON data file in the same directory as the promise
	  # Note: The file size here must be bigger than the data file, up to
	  # the size limit that cfengine allows.
	  "passwords_dbas" data => parsejson(readfile("$(this.promise_dirname)/passwords_dbas.json",4096));

	  # Combine the two data blobs into one if we want to use it later
	  "passwords_all" data => mergedata(passwords_sysadmins,passwords_dbas);

	methods:
		# Call the parameterized bundle (below) to set the sysadmin
		# passwords
		"set passwords_sysadmins" usebundle => set_passwords("set_users_password.passwords_sysadmins");

		# Call the parameterized bundle (below) to set the dba's
		# passwords
		"set passwords_dbas" usebundle => set_passwords("set_users_password.passwords_dbas");

		 # Or we could do a single call with passwords_all:
		 #"set passwords_all" usebundle => set_passwords("set_users_password.passwords_all");

}

# Pass a data object to this bundle to set the user passwords
# The data object should have (at least) attributes of user, hash, and uid.
bundle agent set_passwords( passwords )
{
	vars:
		# I will walk through the passed-in data as $i
		"i" slist => getindices("${passwords}");
		
	users:
	!windows:: # Windows can not set passwords based on hashes

		# for each item in the passed-in data, make a promise
		# relating to a user named by the user attribute
		"${${passwords}[${i}][user]}"
			policy => "present",
			uid => "${${passwords}[${i}][uid]}",
			password => use_hash("${${passwords}[${i}][hash]}");

}

# A parameterized body to set the password to a particular hash value
# Note that cfengine does not check to make sure it is a meaningful or
# acceptable value to the OS, it just sticks it into the right place.
body password use_hash(hash) {
	format => hash; 
	data => "$(hash)";
}
